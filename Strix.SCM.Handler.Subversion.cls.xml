<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Strix.SCM.Handler.Subversion">
<Super>Strix.SCM.Handler.File</Super>
<TimeCreated>64301,43556.823379</TimeCreated>

<Property name="SvnPath">
<Description>
Full path and filename of the SVN executable.</Description>
<Type>%String</Type>
</Property>

<Method name="UserAction">
<FormalSpec><![CDATA[Type:%Integer,Name:%String,InternalName:%String,SelectedText:%String,&Action:%String,&Target:%String,&Msg:%String,&Reload:%Boolean]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	;Assume we won't handle this action
	Set Action = 0, Target = "", Msg = "", Reload = 0
	
	;Studio-generated action (as opposed to menu item)
	If Type = 1 Quit $$$OK
	
	;===== Actions that are always allowed
	
	; (none here)
	
	
	;===== Get project
	
	If InternalName = "" Quit $$$OK
	
	#dim Project As Strix.SCM.Project
	If '##class(Strix.SCM.Project).CheckItem(.InternalName, .Project, .AsItem, .AsStorage, .Msg) {
		If Msg '= "" Set Action = 6 ; MesssageBox
		Quit $$$OK
	}
	
	
	;== Show revisions
	
	If Name = "Source control,ShowRevisions", AsItem {
		Set Filename = Project.GetExportFilename(InternalName, , , .Status)
		If 'Status Quit Status
		
		Set Output = ..GetCommandOutput("log """_Filename_"""", , .ErrorMsg)
		If ErrorMsg '= "" Write ErrorMsg,! Quit $$$OK
		
		Write !,"Revision history for "_InternalName_":",!
		For i = 1:1:Output.Count() {
			Set Line = Output.GetAt(i)
			
			If Line ?1."-" Continue
			If Line = "" Continue
			
			;Indent comment line(s)
			If Line '? 1"r"1.N1.E Write "  "
			
			Write Line,!
		}
		
		Quit $$$OK
	}
	
	;===== Fallback to File handler
	
	Quit ##super(Type, Name, InternalName, SelectedText, .Action, .Target, .Msg, .Reload)
]]></Implementation>
</Method>

<Method name="AfterUserAction">
<FormalSpec><![CDATA[Type:%Integer,Name:%String,InternalName:%String,Answer:%Integer,Msg:%String="",&Reload:%Boolean]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	;Assume we won't handle this action
	Set Action = 0, Target = "", Msg = "", Reload = 0
	
	;Studio-generated action (as opposed to menu item)
	If Type = 1 Quit $$$OK
	
	;User says no
	If Answer = 0 Quit $$$OK  ; No
	If Answer = 2 Quit $$$OK  ; Cancel
	
	;===== Get project
	
	;#dim Project As Strix.SCM.Project
	;If InternalName = "" Quit $$$OK
	;If '##class(Strix.SCM.Project).CheckItem(.InternalName, .Project, .AsItem, .AsStorage, .Msg) Quit $$$OK
	
	
	;===== Fallback to File handler
	
	Quit ##super(Type, Name, InternalName, Answer, .Msg, .Reload)
]]></Implementation>
</Method>

<Method name="OnMenuItem">
<Description>
Called when a menu item is to be displayed; determines what to display,
and whether the item should be enabled or not.</Description>
<FormalSpec><![CDATA[MenuName:%String,InternalName:%String,SelectedText:%String,&Enabled:%Boolean,&DisplayName:%String]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set ItemName = $Piece(MenuName, ",", 2)
	Set Name = $Case(ItemName,
	  "ShowRevisions": "Show revisions for "_InternalName_"",
	  : ""
	)
	
	;If we didn't handle this item, fall back to super
	If Name = "" Quit ##super(MenuName, InternalName, SelectedText, .Enabled, .DisplayName)
	Set DisplayName = Name
	
	#dim Project As Strix.SCM.Project
	If ##class(Strix.SCM.Project).CheckItem(.InternalName, .Project, .AsItem, .AsStorage) {
		;Enabled if known as item, otherwise not
		Set Enabled = $Select(AsItem: 1, 1: 0)
		
	}
	
	Quit $$$OK
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
// =====

]]></Content>
</UDLText>

<Method name="GetMenuItems">
<ClassMethod>1</ClassMethod>
<FormalSpec>MenuName:%String,InternalName:%String,SelectedText:%String,*Status:%Status</FormalSpec>
<ReturnType>%List</ReturnType>
<Implementation><![CDATA[
	;Get items for file handler
	Set Items = ##super(MenuName, InternalName, SelectedText, .Status)
	If 'Status Quit Items
	
	;Append subversion-specific items
	Set Items = Items_$lb(
	  $lb("", 1, 0, 1),
	  $lb("ShowRevisions", 1, 1, 0)
	)
	
	Quit Items
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
// =====

]]></Content>
</UDLText>

<Method name="GetCommandOutput">
<Description>
Returns the output of an svn command (to be passed in without
specifying the executable path). If Split is true, returns it
as a list of lines, already stripped. If an error occurs, the
output parameter ErrorMsg will contain information. A timeout
may occur on the Read if Cache does not have permission to
read the repository.</Description>
<FormalSpec>Command:%String,Split:%Boolean=1,*ErrorMsg</FormalSpec>
<ReturnType>%ListOfObjects</ReturnType>
<Implementation><![CDATA[
	Set $ZTrap = "Error"
	
	Set IO = $IO
	Set ErrorMsg = ""
	
	; Prefix command with normalized path to svn executable
	Set Executable = ##class(%File).NormalizeFilenameWithSpaces(..SvnPath)
	Set Command = Executable_" "_Command
	
	; Use a so-called command pipe to avoid the maximum length of 256 for
	; standard pipe commands.
	Set Pipe = "|CPIPE|"_$Job
	
	; Open it
	Open Pipe:(Command:"QRU"):5
	If '$Test Set ErrorMsg = "Error: timeout occurred opening pipe to "_Command Quit ""
	
	; Get the svn data
	Use Pipe
	Read Output:5
	Set Timedout = '$Test
	Use IO
	Close Pipe
	If Timedout Set ErrorMsg = "Error: timeout occurred reading pipe "_Command Quit ""
	
	; If we're not to split in lines we're done
	If 'Split Quit Output
	
	; Split and strip
	Set Result = ##class(%ListOfDataTypes).%New()
	For i = 1:1:$Length(Output, $Char(10)) {
		Set Line = $Piece(Output, $Char(10), i)
		Set Line = $ZStrip(Line, "<>WC")
		Do Result.Insert(Line)
	}
	
	Quit Result
	
Error
	Set $ZTrap = ""
	Use IO
	Set ErrorMsg = "Error trapped reading pipe: "_$ZError
	Quit ""
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
// =====

]]></Content>
</UDLText>

<Method name="%OnNew">
<Private>1</Private>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	; ToDo: Get data from configuration
	Set ..SvnPath = "C:\Program Files\TortoiseSVN\bin\svn.exe"
	Quit $$$OK
]]></Implementation>
</Method>
</Class>
</Export>
